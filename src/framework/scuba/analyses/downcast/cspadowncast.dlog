# Copyright (c) 2008-2010, Intel Corporation.
# Copyright (c) 2006-2007, The Trustees of Stanford University.
# All rights reserved.
# Licensed under the terms of the New BSD License.


# name=cspa-downcast-dlog

.include "H.dom"
.include "M.dom"
.include "T.dom"
.include "V.dom"
.include "C.dom"

.bddvarorder M0_T0_V0xV1_T1_C0xH0xC1

###
# Relations
###

MobjVarAsgnInst(m:M,l:V,r:V) input
reachableCM(c:C,m:M) input
reachableLR(l:V,r:V)
CVC(c:C,v:V,o:C) input
CH(c:C,h:H) input
sub(s:T1,t:T0) input
VT(v:V0,t:T0) input
HT(h:H0,t:T1) input

#v is declared in method m.
MV(m:M0,v:V0) input
## v is downcast to type t in method m.
dcm(m:M,v:V,t:T) output
## dump out the points-to set related to downcast.
dcmVH(v:V,h:H) output

## used to filter library methods
librariesM(m:M0)input


reachableappsCM(m:M0)output

ptsVH(v:V,h:H) output
ptsVT(v:V,t:T) output
downcast(v:V,t:T) output
unsafeDowncast(v:V,t:T) output
safeDowncast(v:V,t:T) output

## locals that belong to application.
AppLocal(v:V) output
## downcast locals
DcLocal(v:V) output



#reachableLR(l,r) :- MobjVarAsgnInst(m,l,r), reachableCM(_,m).


reachableappsCM(m) :- reachableCM(_,m), !librariesM(m).
AppLocal(v) :- reachableappsCM(m), MV(m,v).

##only care about application level downcast
reachableLR(l,r) :- MobjVarAsgnInst(m,l,r), reachableappsCM(m).

ptsVH(v,h) :- CVC(_,v,o), CH(o,h).
ptsVT(v,t) :- ptsVH(v,h), HT(h,t).

downcast(r,t) :- reachableLR(l,r), \
	VT(l,t), VT(r,t2), !sub(t2,t).
	
dcm(m,v,t) :- MV(m,v), downcast(v,t). 
dcmVH(v,h) :- ptsVH(v,h), downcast(v,_).
DcLocal(v) :- AppLocal(v), downcast(v,_).

unsafeDowncast(v,t) :- downcast(v,t), ptsVT(v,t2), !sub(t2,t).
safeDowncast(v,t) :- downcast(v,t), !unsafeDowncast(v,t).
